DialogMonitorOPS.unRegisterNotification id:#eyeInTheSky

fn dmnotification = 
(
WindowHandle = DialogMonitorOPS.GetWindowHandle()
WindowName = UIAccessor.GetWindowText WindowHandle
if WindowName != undefined then
   (
   if (findString WindowName "Save UVW") != undefined then
      (
      cWindows = UIAccessor.GetChildWindows WindowHandle
      entryField = 0
      for t = 1 to cWindows.count do
         (
         if cWindows[t] != 0 then
            (
            if (UIAccessor.GetWindowClassName cWindows[t]) == "ComboBoxEx32" then
               entryField = cWindows[t]
            )
         )
      if entryField != 0 then
         (
         UIAccessor.SetWindowText entryField "C:\\Temp\\tmp.uvw"
         UIAccessor.PressButtonByName WindowHandle "&Save"
         )
      )
   )
true
)

DialogMonitorOPS.RegisterNotification dmnotification id:#eyeInTheSky
DialogMonitorOPS.Enabled = true

$.modifiers[#Unwrap_UVW].unwrap.save()

DialogMonitorOPS.Enabled = false
DialogMonitorOPS.unRegisterNotification id:#eyeInTheSky